import org.apache.tools.ant.filters.ReplaceTokens

import java.time.Instant

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    group project.parent == null ? 'fr.redstonneur1256' : project.parent.group + '.' + project.parent.name
    version System.getenv('GITHUB_VERSION') ?: 'dev'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.mc-skyplex.net/releases' }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    artifacts {
        archives(sourcesJar)
        archives(javadocJar)
    }

    sourceSets {
        main.java.srcDirs = ['src/']
        main.resources.srcDirs = ['res/']
        test.java.srcDirs = []
        test.resources.srcDirs = []
    }

    processResources {
        filesMatching((Iterable<String>) ['*.properties', 'mod.json']) {
            filter(ReplaceTokens, tokens: [
                    version: version,
                    build  : System.getenv('GITHUB_SHA') ?: 'dev',
                    built  : Instant.now().toString()
            ])
        }
        outputs.upToDateWhen { false }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    shadowJar.archiveName(project.name + '.jar')

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }
}
